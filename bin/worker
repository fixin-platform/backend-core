#!/usr/bin/env coffee

path = require "path"
parse = require "path-parse"
yargs = require "yargs"
domain = require "domain"
WorkerFactory = require "../lib/WorkerFactory"

argv = yargs
  .usage('Usage: $0 [options] pathToJob1 ... pathToJobN')
  .options(
    "a":
      alias: "api"
      type: "string"
      demand: true
    "d":
      alias: "addr"
      type: "string"
      demand: true
      default: "tcp://localhost:55550"
  )
  .demand(1)
  .argv

jobs = {}
for jobPath in argv._
  name = parse(jobPath).name
  jobs[name] = require path.resolve(process.cwd(), jobPath)

worker = WorkerFactory.create(argv.addr, argv.api, {}, jobs)

worker.on "stop", ->
  process.exit(worker.exitCode) # only exit after we have closed the sockets, etc

shutdown = (exitCode) ->
  worker.exitCode = exitCode
  worker.stop()

worker.on "error", (msg) ->
  console.error(msg)
  switch msg
    when "ERR_MSG_TYPE_INVALID", "ERR_MSG_HEADER" # internal worker errors
      # winter is coming... but don't shutdown just yet
    else
      shutdown(1) # something really bad happened, let's shutdown

(require "death")(-> shutdown(0)) # handle SIGINT, SIGQUIT, SIGTERM, return normal exit code (signal probably sent by admin)

worker.start("vodka") # hope it helps